buildscript {
	ext {
		// Plugin API
		pluginVersion = '4.0.0'

		// Java compile
		javaTargetVersion = JavaVersion.VERSION_1_8
	}
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath "gradle.plugin.com.github.jengelman.gradle.plugins:shadow:7.0.0"
		classpath "com.diffplug.spotless:spotless-plugin-gradle:6.12.1"
	}
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
apply plugin: 'com.diffplug.spotless'

group = 'net.KabOOm356'
version = pluginVersion

description = """"""

sourceCompatibility = javaTargetVersion
targetCompatibility = javaTargetVersion

wrapper {
	gradleVersion = '7.4.1'
	distributionType = Wrapper.DistributionType.ALL
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
	options.compilerArgs << '-Xlint:unchecked'
	options.deprecation = true
}

shadowJar {
	relocate 'org.bstats', 'net.KabOOm356.bstats'
	archiveFileName = 'Reporter.jar'
}
build.dependsOn shadowJar

spotless {
	ratchetFrom 'origin/master'

	format 'misc', {
		target '*.gradle', '*.md', '.gitignore'

		trimTrailingWhitespace()
		indentWithTabs()
		endWithNewline()
	}

	java {
		googleJavaFormat()
	}
}

repositories {
	mavenCentral()
	maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
	maven {
		url 'http://nexus.hc.to/content/repositories/pub_releases'
		allowInsecureProtocol = true
	}
	maven { url 'https://repo.codemc.org/repository/maven-public' }
}

dependencies {
	implementation group: 'org.bstats', name: 'bstats-bukkit', version: '3.0.0'

	compileOnly group: 'org.spigotmc', name: 'spigot-api', version: '1.19.3-R0.1-SNAPSHOT'
	compileOnly group: 'org.spigotmc', name: 'plugin-annotations', version: '1.2.3-SNAPSHOT'
	compileOnly group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.19.0'
	compileOnly group: 'net.milkbowl.vault', name: 'VaultAPI', version: '1.7'

	testImplementation group: 'junit', name: 'junit', version: '4.13.2'
	testImplementation group: 'org.mockito', name: 'mockito-inline', version: '4.11.0'
	testImplementation group: 'org.spigotmc', name: 'spigot-api', version: '1.19.3-R0.1-SNAPSHOT'
	testImplementation group: 'org.spigotmc', name: 'plugin-annotations', version: '1.2.3-SNAPSHOT'
	testImplementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.19.0'
	testImplementation group: 'net.milkbowl.vault', name: 'VaultAPI', version: '1.7'

	annotationProcessor group: 'org.spigotmc', name: 'plugin-annotations', version: '1.2.3-SNAPSHOT'
}
